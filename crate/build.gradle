plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    def versionPropsFile = file('version.properties')
    def patchVersion = 0

    Properties versionProps = new Properties()

    if (!versionPropsFile.exists())
    {
        versionProps['VERSION_PATCH'] = "0"
        versionProps['VERSION_BUILD'] = "0"
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    if(versionPropsFile.canRead())
    {
        if('assembleRelease' in gradle.startParameter.taskNames)
        {
            versionProps.load(new FileInputStream(versionPropsFile))

            if(patchVersion != versionProps['VERSION_PATCH'].toInteger())
            {
                versionProps['VERSION_PATCH'] = (versionProps['VERSION_PATCH'].toInteger() + 1).toString()
                versionProps['VERSION_BUILD'] = 0
            }
            else versionProps['VERSION_BUILD'] = (versionProps['VERSION_BUILD'].toInteger() + 1).toString()

            versionProps.store(versionPropsFile.newWriter(), null)
        }

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 30
            versionCode 1
            versionName "1.0.${versionProps['VERSION_PATCH']}.${versionProps['VERSION_BUILD']}"

            ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'

            setProperty("archivesBaseName", "blacksmith-$versionName")
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"
        }
    } else throw new FileNotFoundException("Could not read version.properties!")

    buildTypes {
        release {
            multiDexEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = '1.8' }

    externalNativeBuild.ndkBuild.path 'src/main/jni/Android.mk'
    ndkVersion ndkVersion
//    sourceSets.main.jniLibs.srcDirs = ['libs']
}

//ext.versions = [
//        hilt               : '1.0.0-alpha03',
//        dagger             : '2.31.2-alpha',
//]

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    //implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.3.0'
    // SharedPreference bug temp fix
//    implementation ('com.google.crypto.tink:tink-android') { version {strictly '1.4.0-rc2'} }

    // Hilt
//    implementation "com.google.dagger:hilt-android:$versions.dagger"
//    kapt "com.google.dagger:hilt-android-compiler:$versions.dagger"

    // Misc
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/robinlow/util-android")
                credentials {
                    username = GITHUB_USER
                    password = GITHUB_WRITE_ACCESS_TOKEN
                }
            }
        }

        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can customize attributes of the publication here or in module's build.gradle file.
                groupId 'com.ownapp.util'
                artifactId 'crate'
                version '1.0.3'
            }
        }
    }
}